#
# Executes commands at login post-zshrc.
#
# Authors:
#   Sorin Ionescu <sorin.ionescu@gmail.com>
#

# Execute code that does not affect the current session in the background.
{
  # Compile the completion dump to increase startup speed.
  zcompdump="${XDG_CACHE_HOME:-$HOME/.cache}/prezto/zcompdump"
  if [[ -s "$zcompdump" && (! -s "${zcompdump}.zwc" || "$zcompdump" -nt "${zcompdump}.zwc") ]]; then
    if command mkdir "${zcompdump}.zwc.lock" 2>/dev/null; then
      zcompile "$zcompdump"
      command rmdir  "${zcompdump}.zwc.lock" 2>/dev/null
    fi
  fi
} &!

# Execute code only if STDERR is bound to a TTY.
if [[ -o INTERACTIVE && -t 2 ]]; then
  # Query terminal size; useful for serial lines.
  if [ -x /usr/bin/resizewin ]; then
    /usr/bin/resizewin -z
  fi

  # Print a random, hopefully interesting, adage.
  if (( $+commands[fortune] )); then
    if [ -r /usr/local/share/games/fortune/ferengi_rules_of_acquisition ]; then
      fortune ferengi_rules_of_acquisition
    elif [ -r /usr/local/share/games/fortune/startrek ]; then
      fortune startrek
    else
      fortune -s
    fi
    print
  fi

fi >&2

if [[ "${$(tty):0:8}" = '/dev/tty' && -z "${SSH_AGENT_PID}" ]]; then
  eval $(ssh-agent)
elif (( $+commands[wsl-ssh-agent.sh] )); then
  eval $(wsl-ssh-agent.sh)
elif [[ -r ~/bin/ssh-auth-sock-update.sh ]]; then
  source ~/bin/ssh-auth-sock-update.sh
fi

# Local Variables:
# mode: sh
# sh-shell: zsh
# sh-basic-offset: 2
# End:
